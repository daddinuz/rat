/*
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/.
 */

Program = _{
    SOI ~ (Definition | Word | Expression)* ~ EOI
}

Definition = {
    Colon ~ Word ~ (Word | Expression)* ~ Semicolon
}

Colon = @{
    ":" ~ &BREAK
}

Semicolon = @{
    ";" ~ &BREAK
}

Word = @{
    // TODO: this must always be kept in sync with `Word::new`
    ("!" | "%" | "&" | "*" | "+" | "-" | "/" | "<" | "=" | ">" | "?" | 'A'..'Z' | "^" | "_" | 'a'..'z' | "|" | "~")+ ~ &BREAK
}

Expression = {
    (Boolean | Decimal | Integer | Quote | Signal | String | Symbol)
}

Boolean = @{
    ("⊥" | "⊤") ~ &BREAK
}

Decimal = @{
    ("+" | "-")? ~ "∞" | "0.NaN" | (((ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) | "0") ~ ((^"e" ~ ("+" | "-") ~ ASCII_DIGIT+) | "." ~ ASCII_DIGIT* ~ (^"e" ~ ("+" | "-") ~ ASCII_DIGIT+) | "." ~ ASCII_DIGIT+)) ~ &BREAK
}

Integer = @{
    ("+" | "-")? ~ ((ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) | "0") ~ &BREAK
}

Quote = {
    LeftSquareBracket ~ (Word | Expression)* ~ RightSquareBracket
}

LeftSquareBracket = @{
    "["
}

RightSquareBracket = @{
    "]" ~ &BREAK
}

Signal = @{
    "$" ~ ('A'..'Z' | 'a'..'z')+ ~ &BREAK
}

String = ${
    "\"" ~ StringUnicodeScalarValue* ~ "\"" ~ &BREAK
}

StringUnicodeScalarValue = @{
    (!("\"" | "\\" | NEWLINE) ~ ANY | "\\" ~ ("\"" | "'" | "\\" | "n" | "r" | "t" | ("u{" ~ ASCII_HEX_DIGIT{1, 4} ~ "}")))
}

Symbol = ${
    "'" ~ SymbolUnicodeScalarValue* ~ "'" ~ &BREAK
}

SymbolUnicodeScalarValue = @{
    (!("'" | "\\" | NEWLINE) ~ ANY | "\\" ~ ("\"" | "'" | "\\" | "n" | "r" | "t" | ("u{" ~ ASCII_HEX_DIGIT{1, 4} ~ "}")))
}

WHITESPACE = _{ NEWLINE | "\t" | " " }
COMMENT    = _{ "#" ~ (!NEWLINE ~ ANY)* }
BREAK      = _{ WHITESPACE | "]" | EOI }
