/*
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/.
 */

Program = _{
    SOI ~ (Definition | Phrase)* ~ EOI
}

Definition = {
    (Division | Colon) ~ Word ~ ((At ~ Locution) | (LeftArrow ~ Phrase)) ~ Semicolon
}

Phrase = {
    (Locution | Expression)+
}

Word = @{
    // TODO: this must always be kept in sync with `Word`
    ( ("-" | '0'..'9' | "_")*
    ~ ('A'..'Z' | 'a'..'z')
    ~ ("-" | '0'..'9' | 'A'..'Z' | "_" | 'a'..'z')*
    ~ ("!" | "?")?
    )
}

Locution = @{
    // TODO: this must always be kept in sync with `Locution`
    Word ~ ("\\" ~ Word)*
}

Expression = {
    (Boolean | Decimal | Integer | Quote | String | Symbol)
}

Boolean = @{
    ("⊥" | "⊤")
}

Decimal = @{
    ("+" | "-")? ~ "∞" | "0.NaN" | (((ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) | "0") ~ ((^"e" ~ ("+" | "-") ~ ASCII_DIGIT+) | "." ~ ASCII_DIGIT* ~ (^"e" ~ ("+" | "-") ~ ASCII_DIGIT+) | "." ~ ASCII_DIGIT+))
}

Integer = @{
    ("+" | "-")? ~ ((ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) | "0")
}

Quote = {
    LeftSquareBracket ~ Phrase? ~ RightSquareBracket
}

String = ${
    "\"" ~ StringUnicodeScalarValue* ~ "\""
}

StringUnicodeScalarValue = @{
    (!("\"" | "\\" | NEWLINE) ~ ANY | "\\" ~ ("\"" | "'" | "\\" | "n" | "r" | "t" | ("u{" ~ ASCII_HEX_DIGIT{1, 4} ~ "}")))
}

Symbol = ${
    "'" ~ SymbolUnicodeScalarValue* ~ "'"
}

SymbolUnicodeScalarValue = @{
    (!("'" | "\\" | NEWLINE) ~ ANY | "\\" ~ ("\"" | "'" | "\\" | "n" | "r" | "t" | ("u{" ~ ASCII_HEX_DIGIT{1, 4} ~ "}")))
}

At = @{
    "@"
}

Colon = @{
    ":"
}

Division = @{
    "÷"
}

LeftArrow = @{
    "←"
}

Semicolon = @{
    ";"
}

LeftSquareBracket = @{
    "["
}

RightSquareBracket = @{
    "]"
}

WHITESPACE = _{ NEWLINE | "\t" | " " }
COMMENT    = _{ "#" ~ (!NEWLINE ~ ANY)* }
