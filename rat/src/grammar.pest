/*
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/.
 */

Program = _{
    SOI ~ (Definition | Phrase)* ~ EOI
}

Definition = {
    (Division | Colon) ~ Word ~ ((At ~ Locution) | (LeftArrow ~ Phrase)) ~ Semicolon
}

Phrase = {
    (Locution | Expression)+
}

Word = @{
    _Word ~ &BREAK
}

_Word = @{
    // TODO: this must always be kept in sync with `Word`
    ("!" | "%" | "&" | "*" | "+" | "-" | "/" | "<" | "=" | ">" | "?" | 'A'..'Z' | "^" | "_" | 'a'..'z' | "|" | "~")+
}

Locution = @{
    // TODO: this must always be kept in sync with `Locution`
    _Word ~ ("\\" ~ _Word)* ~ &BREAK
}

Expression = {
    (Boolean | Decimal | Integer | Quote | Signal | String | Symbol)
}

Boolean = @{
    ("⊥" | "⊤") ~ &BREAK
}

Decimal = @{
    ("+" | "-")? ~ "∞" | "0.NaN" | (((ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) | "0") ~ ((^"e" ~ ("+" | "-") ~ ASCII_DIGIT+) | "." ~ ASCII_DIGIT* ~ (^"e" ~ ("+" | "-") ~ ASCII_DIGIT+) | "." ~ ASCII_DIGIT+)) ~ &BREAK
}

Integer = @{
    ("+" | "-")? ~ ((ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) | "0") ~ &BREAK
}

Quote = {
    LeftSquareBracket ~ Phrase? ~ RightSquareBracket
}

Signal = @{
    "$" ~ ('A'..'Z' | 'a'..'z')+ ~ &BREAK
}

String = ${
    "\"" ~ StringUnicodeScalarValue* ~ "\"" ~ &BREAK
}

StringUnicodeScalarValue = @{
    (!("\"" | "\\" | NEWLINE) ~ ANY | "\\" ~ ("\"" | "'" | "\\" | "n" | "r" | "t" | ("u{" ~ ASCII_HEX_DIGIT{1, 4} ~ "}")))
}

Symbol = ${
    "'" ~ SymbolUnicodeScalarValue* ~ "'" ~ &BREAK
}

SymbolUnicodeScalarValue = @{
    (!("'" | "\\" | NEWLINE) ~ ANY | "\\" ~ ("\"" | "'" | "\\" | "n" | "r" | "t" | ("u{" ~ ASCII_HEX_DIGIT{1, 4} ~ "}")))
}

At = @{
    "@" ~ &BREAK
}

Colon = @{
    ":" ~ &BREAK
}

Division = @{
    "÷" ~ &BREAK
}

LeftArrow = @{
    "←" ~ &BREAK
}

Semicolon = @{
    ";" ~ &BREAK
}

LeftSquareBracket = @{
    "["
}

RightSquareBracket = @{
    "]" ~ &BREAK
}

WHITESPACE = _{ NEWLINE | "\t" | " " }
COMMENT    = _{ "#" ~ (!NEWLINE ~ ANY)* }
BREAK      = _{ WHITESPACE | "]" | EOI }
